# $Id: PKGBUILD 77704 2012-10-13 15:48:59Z bpiotrowski $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>

pkgname=tmux
pkgver=1.7
pkgrel=1
pkgdesc='A terminal multiplexer'
url='http://tmux.sourceforge.net/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('ncurses' 'libevent')
source=(http://downloads.sourceforge.net/tmux/tmux-$pkgver.tar.gz
		LICENSE
        borders.patch
        utf8_width.patch
        copy_cursor_width.patch)
md5sums=('2c48fb9beb22eedba7a5de3b78dd0c03'
         '71601bc37fa44e4395580b321963018e'
         'ebf01f342a3007f9515e50ddf4c883cf'
         'ed195ffa2573ed8a503f20f823a47006'
         '7823df972d66b968100ebf18d08e8613')

build() {
	cd "$srcdir/$pkgname-$pkgver"
    patch -p1 -i ../borders.patch
    patch -p1 -i ../utf8_width.patch
    patch -p1 -i ../copy_cursor_width.patch
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make install DESTDIR=$pkgdir
	install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/tmux/LICENSE"

	install -dm755 "$pkgdir/usr/share/tmux/"
	install -m644 examples/* "$pkgdir/usr/share/tmux/"
	install -Dm644 examples/tmux.vim "$pkgdir/usr/share/vim/vimfiles/syntax/tmux.vim"

	install -d $pkgdir/usr/share/bash-completion/completions/
	mv $pkgdir/usr/share/tmux/bash_completion_tmux.sh $pkgdir/usr/share/bash-completion/completions/tmux
}
